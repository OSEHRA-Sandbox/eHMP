HMPZUIP ;ASMR/JD - RPCs for HMP UI CONTEXT option;Sep 7, 2016@18:14:07
 ;;2.0;ENTERPRISE HEALTH MANAGEMENT PLATFORM;**;November 30,2015;Build 63
 ;****** This code is for eHMP development only. DO NOT DEPLOY TO SITES ******
 ;
 ;DE6589 - JD - 8/30/16: This routine guarantees a basic set of RPCs (listed in the RPCS tag)
 ;exist in the HMP UI CONTEXT option.
 ;
 Q
 ;
 ;Post-init logic
POST	;
 D MES^XPDUTL($T(+0)_" post-init routine started "_$$HTE^XLFDT($H))
 N HMPCNT,HMPRPCNM,HMPUICIE
 S HMPUICIE=$O(^DIC(19,"B","HMP UI CONTEXT",""))  ;HMP UI CONTEXT option IEN
 I 'HMPUICIE D MES^XPDUTL("HMP UI CONTEXT option does not exist.") Q
 ;Loop through the list of RPCs
 F HMPCNT=1:1 S HMPRPCNM=$P($T(RPCS+HMPCNT),";;",2) Q:$L(HMPRPCNM)=0  D
 .N HMPFDA,HMPRPCIE,HMPERR
 .;Does the RPC exist in Remote Procedure file (#8994)?
 .S HMPRPCIE=$O(^XWB(8994,"B",HMPRPCNM,""))  ;IEN of the RPC
 .I 'HMPRPCIE D MES^XPDUTL("'"_HMPRPCNM_"' RPC does not exist.  Nothing to add.") Q
 .;Skip if the RPC already exists in the option skip it
 .I $O(^DIC(19,HMPUICIE,"RPC","B",HMPRPCIE,"")) D MES^XPDUTL("'"_HMPRPCNM_"' RPC already exists in the option.  Skipped.") Q
 .;Add the RPC to the option
 .S HMPFDA(19.05,"+1,"_HMPUICIE_",",.01)=HMPRPCIE
 .D UPDATE^DIE("","HMPFDA","","HMPERR")
 .I '$D(HMPERR) D MES^XPDUTL("'"_HMPRPCNM_"' RPC was added.")
 .;Unable to add the RPC due to Fileman error
 .I $D(HMPERR) D
 ..D MES^XPDUTL("FileMan error when adding '"_HMPRPCNM_"' RPC.")
 ..N V S V="HMPERR" F  S V=$Q(@V) Q:V=""  D MES^XPDUTL(V_" = "_@V)
 D MES^XPDUTL($T(+0)_" post-init routine ended "_$$HTE^XLFDT($H))
 Q
RPCS ;List of RPCs that need to be in HMP UI CONTEXT option
 ;;GMV ADD VM
 ;;GMV GET CURRENT TIME
 ;;GMV MARK ERROR
 ;;GMV VITALS/CAT/QUAL
 ;;HMP APPOINTMENTS
 ;;HMP CHKXTMP
 ;;HMP DATA VERSION
 ;;HMP DEFAULT PATIENT LIST
 ;;HMP DELETE OBJECT
 ;;HMP GET OBJECT
 ;;HMP GET PATIENT DATA
 ;;HMP GET PATIENT DATA JSON
 ;;HMP GET RELATED ORDERS
 ;;HMP GLOBAL SIZE
 ;;HMP INPATIENTS
 ;;HMP MED ORDER CHECKS
 ;;HMP PATIENT ADMIT SYNC
 ;;HMP PATIENT SCHED SYNC
 ;;HMP PATIENT SELECT
 ;;HMP PUT DEMOGRAPHICS
 ;;HMP PUT OBJECT
 ;;HMP PUT OPERATIONAL DATA
 ;;HMP PUT PATIENT DATA
 ;;HMP SUBSCRIBE
 ;;HMP TIU LONG LIST OF TITLES
 ;;HMP WRITEBACK ALLERGY
 ;;HMP WRITEBACK ALLERGY EIE
 ;;HMP WRITEBACK ENCOUNTERS
 ;;HMP WRITEBACK SIGN ORDERS
 ;;HMP WRITEBACK VITAL
 ;;HMP WRITEBACK VITAL EIE
 ;;HMPCPRS RPC
 ;;HMPCRPC RPC
 ;;HMPCRPC RPCCHAIN
 ;;HMPFPTC CHKS
 ;;HMPFPTC LOG
 ;;ORCNOTE GET TOTAL
 ;;ORQOR DETAIL
 ;;ORQPT CLINIC PATIENTS
 ;;ORQPT DEFAULT PATIENT LIST
 ;;ORQPT PROVIDER PATIENTS
 ;;ORQPT SPECIALTIES
 ;;ORQPT SPECIALTY PATIENTS
 ;;ORQPT WARD PATIENTS
 ;;ORQPT WARDS
 ;;ORQQPL ADD SAVE
 ;;ORQQPL DELETE
 ;;ORQQPL EDIT LOAD
 ;;ORQQPL EDIT SAVE
 ;;ORQQPL USER PROB LIST
 ;;ORQQPL4 LEX
 ;;ORQQPX REMINDER DETAIL
 ;;ORQQVI NOTEVIT
 ;;ORWCV VST
 ;;ORWDAL32 ALLERGY MATCH
 ;;ORWDAL32 CLINUSER
 ;;ORWDAL32 SAVE ALLERGY
 ;;ORWDAL32 SYMPTOMS
 ;;ORWDRA32 RAORDITM
 ;;ORWLEX GETFREQ
 ;;ORWLEX GETI10DX
 ;;ORWLR CUMULATIVE REPORT
 ;;ORWLRR INTERIM
 ;;ORWPCE GETSVC
 ;;ORWPCE HASVISIT
 ;;ORWPCE LEX
 ;;ORWPCE LEXCODE
 ;;ORWPCE NOTEVSTR
 ;;ORWPCE PCE4NOTE
 ;;ORWPCE SAVE
 ;;ORWPCE SCSEL
 ;;ORWPT ADMITLST
 ;;ORWPT APPTLST
 ;;ORWPT BYWARD
 ;;ORWPT LIST ALL
 ;;ORWRP COLUMN HEADERS
 ;;ORWRP REPORT LISTS
 ;;ORWRP REPORT TEXT
 ;;ORWRP3 EXPAND COLUMNS
 ;;ORWU CLINLOC
 ;;ORWU DT
 ;;ORWU EXTNAME
 ;;ORWU NEWPERS
 ;;ORWU USERINFO
 ;;ORWU VALIDSIG
 ;;ORWU1 NEWLOC
 ;;PX SAVE DATA
 ;;PXVIMM ADMIN ROUTE
 ;;PXVIMM ADMIN SITE
 ;;PXVIMM IMM LOT
 ;;PXVIMM IMM MAN
 ;;PXVIMM IMMDATA
 ;;PXVIMM INFO SOURCE
 ;;PXVIMM VIS
 ;;TIU AUTHORIZATION
 ;;TIU CREATE ADDENDUM RECORD
 ;;TIU CREATE RECORD
 ;;TIU DELETE RECORD
 ;;TIU DOCUMENTS BY CONTEXT
 ;;TIU GET DOCUMENT TITLE
 ;;TIU GET RECORD TEXT
 ;;TIU GET REQUEST
 ;;TIU IS THIS A CONSULT?
 ;;TIU IS THIS A SURGERY?
 ;;TIU ISPRF
 ;;TIU LOCK RECORD
 ;;TIU LONG LIST OF TITLES
 ;;TIU REQUIRES COSIGNATURE
 ;;TIU SIGN RECORD
 ;;TIU UNLOCK RECORD
 ;;VAFC LOCAL GETCORRESPONDINGIDS
 ;;XHD GET PARAMETER DEF LIST
