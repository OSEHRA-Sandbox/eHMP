ZZGENAPT ; SEB - Generate appointments for all clinics
 ;;1.0;VistA Data Support;;JUNE 20, 2015
GEN(DFN,STARTDT,COPYDFN,COPYDT,COPYCLIN) ; Generate appointments for a given patient
 S (DAY,CLINID)=0
 F  S CLINID=$O(^SC(CLINID)) Q:+CLINID=0  D  I RETURN]"" Q
 . S CLINDATA=$G(^SC(CLINID,0)) I "CZ"'[$P(CLINDATA,"^",3) Q
 . S HOURS=(CLINID\2*.01)#.24,MINUTES=(CLINID#2*.003) S:HOURS#.24=0&(MINUTES=0) DAY=DAY+1
 . S APPTDT=STARTDT+DAY+HOURS+MINUTES
 . S RETURN=""
 . D PATIENT(.RETURN,DFN,CLINID,APPTDT,COPYDFN,COPYDT,COPYCLIN)
 . I RETURN="" D CLINIC(.RETURN,CLINID,DFN,APPTDT,COPYDFN,COPYDT,COPYCLIN)
 . W !,CLINID,?8,APPTDT,?22,RETURN
 . Q
 I RETURN="" S RETURN="1^OK"
 Q
 ;
PATIENT(RETURN,PATID,CLINIC,APPTDT,COPYDFN,COPYDT,COPYCLIN) ; Create appointment in patient file
 S ENTRYDT=$P($G(^DPT(COPYDFN,"S",COPYDT,0)),"^",19)
 I ENTRYDT="" S RETURN="-1^No entry date!" Q
 ;
 I $D(^DPT(PATID,"S",APPTDT)) S RETURN="-1^An appointment already exists at the new date/time!" Q
 M ^DPT(PATID,"S",APPTDT)=^DPT(COPYDFN,"S",COPYDT)
 S $P(^DPT(PATID,"S",APPTDT,0),"^")=CLINIC
 S ^DPT("ASADM",ENTRYDT,PATID,APPTDT)=""
 Q
 ;
CLINIC(RETURN,CLINIC,PATID,APPTDT,COPYDFN,COPYDT,COPYCLIN) ; Create appointment in hospital location file
 F APPTID=1:1 S APPTDATA=$G(^SC(COPYCLIN,"S",COPYDT,1,APPTID,0)) Q:APPTDATA=""!(+APPTDATA=COPYDFN)
 I APPTDATA="" S RETURN="-1^Missing appointment data in clinic file!" Q
 I '$D(^SC(CLINIC,"S",APPTDT)) S ^SC(CLINIC,"S",APPTDT,0)=APPTDT,^SC(CLINIC,"S",APPTDT,1,0)="^44.003PA^^"
 F NEWAPPTID=1:1 I '$D(^SC(CLINIC,"S",APPTDT,1,NEWAPPTID,0)) Q
 M ^SC(CLINIC,"S",APPTDT,1,NEWAPPTID)=^SC(COPYCLIN,"S",COPYDT,1,APPTID)
 S $P(^SC(CLINIC,"S",APPTDT,1,NEWAPPTID,0),"^")=PATID
 Q
 ;
EXEC ; Run the routine to generate a set of past appointments and a set of future appointments
 D GEN(100805,3150601,100861,3150613.08,64)
 D GEN(100805,3200601,100861,3150625.09,62)
 Q
 ;
