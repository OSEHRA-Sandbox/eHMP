ZZROLLDT ; SEB - MUMPS implementation of Rolling Dates
 ;;1.0;VistA Data Support;;JULY 29, 2016;Build 1
ALL S FILE=0
ALL1 S FILE=$O(^ZZROLLDT(FILE)) I FILE="" G QUIT
 I $P($G(^ZZROLLDT(FILE,"INFO")),"^",2)=1 D FILE(FILE)
 G ALL1
 ;
FILE(FILE) S LINE=0
FILE1 S LINE=$O(^ZZROLLDT(FILE,"GLOBAL",LINE)) I LINE="" G DIKDA
 S GLOBAL=^ZZROLLDT(FILE,"GLOBAL",LINE)
 K RETURN S RETURN=0 D PARSE(GLOBAL,$L(GLOBAL),.RETURN)
 S NEWGLOBAL=GLOBAL I RETURN>0 D
 . F CT=RETURN:-1:1 D
 . . S RESULT=RETURN(CT),START=$P(RESULT,"^",2),END=$P(RESULT,"^",3),RESULT=$P(RESULT,"^")
 . . S $E(NEWGLOBAL,START,END)=$$GETNEWDT(RESULT)
 . . Q
 . Q
 S DATA=$G(^ZZROLLDT(FILE,"DATA",LINE))
 K RETURN S RETURN=0 D PARSE(DATA,$L(DATA),.RETURN)
 S NEWDATA=DATA I RETURN>0 D
 . F CT=RETURN:-1:1 D
 . . S RESULT=RETURN(CT),START=$P(RESULT,"^",2),END=$P(RESULT,"^",3),RESULT=$P(RESULT,"^")
 . . S $E(NEWDATA,START,END)=$$GETNEWDT(RESULT)
 . . Q
 . Q
 S @NEWGLOBAL=NEWDATA
 W "."
 G FILE1
 ;
DIKDA N DA,DIK S DIKDA=0
DIKDA1 S DIKDA=$O(^ZZROLLDT(FILE,"DIKDA",DIKDA)) I DIKDA="" G EXECUTE
 S DIKDADATA=^ZZROLLDT(FILE,"DIKDA",DIKDA),DIK="^"_$P(DIKDADATA,"^",2),DA=$P(DIKDADATA,"^",3) D IX^DIK
 G DIKDA1
 ;
EXECUTE S EXECUTE=0
EXEC1 S EXECUTE=$O(^ZZROLLDT(FILE,"EXECUTE",EXECUTE)) I EXECUTE="" G QUIT
 X ^ZZROLLDT(FILE,"EXECUTE",EXECUTE)
 G EXEC1
 ;
QUIT Q
 ;
PARSE(STRING,ORIGLEN,RETURN) ; Parse string for items in {}
 S START=$F(STRING,"{"),END=$F(STRING,"}") I START'=0,END'=0 D
 . S RETURN=RETURN+1,RETURN(RETURN)=$E(STRING,START,END-2)_"^"_(START-1+ORIGLEN-$L(STRING))_"^"_(END-1+ORIGLEN-$L(STRING))
 . D PARSE($E(STRING,END,$L(STRING)),ORIGLEN,.RETURN)
 . Q
 Q
 ;
TOUPPER(STRING) ; Return uppercase string
 S STRING2=$TR(STRING,"abcdefghijklmnopqrstuvwxyz","ABCDEFGHIJKLMNOPQRSTUVWXYZ")
 Q STRING2
 ;
GETNEWDT(RESULT) ; Calculate date based on passed-in value
 S RESULT=$$TOUPPER(RESULT)
 S FLAG="" I "IE"[$E(RESULT,$L(RESULT)) S FLAG=$E(RESULT,$L(RESULT)),RESULT=$E(RESULT,1,$L(RESULT)-1)
 S NEWDATE=$$GETDATE^ZZMOVDT2("",RESULT)
 I FLAG="I" S NEWDATE=9999998-NEWDATE
 I FLAG="E" S Y=NEWDATE D DD^%DT S NEWDATE=Y
 Q NEWDATE
 ;
