HMPROS6 ;SLC/GRR -- Generate Roster Patients
 ;;2.0;ENTERPRISE HEALTH MANAGEMENT PLATFORM;**1**;Sep 01, 2011;Build 49
 ;; Compile Roster
GET(HMPIEN) ;
 ;; Input - HMPIEN is internal entry number of roster
 ;;         HMPOWNER - If this parameter exists, only rosters for this owner will be compiled and passed
 ;; Output - AFTER array contains current patients
 ;
 K HMPLIST,HMPLIST2
 N HMPI,HMPFILT,HMPTYPE,VPERR,HMPRNAME,HMPNY,HMPOP,HMPTAG,HMPLAB,HMPNLIST,BEG,DOB,END,GENDER,ICN,NAME,HMPACT,HMPC
 N HMPCIEN,HMPDNAME,HMPDOB,HMPIII,HMPINM,HMPLIEN,HMPNAME,HMPOIEN,HMPONAME,HMPOWNID,HMPOWNNM,HMPPAT,HMPPIEN,HMPNME,HMPCNT
 N HMPSRCDN,HMPCID,HMPTEXT,HMPTIEN,HMPTLST,HMPVER,HMPWIEN,HMPWNAME,HMPPNME,HMPRCNT,HMPSRCID,X,Y,SSN,HMPZ,HMPIZ,HMPX
 N HMPSYS S HMPSYS=$$GET^XPAR("SYS","HMP SYSTEM NAME")
 S HMPIZ=0
 I $G(HMPIEN)="" S HMPIEN=0
 S (HMPLIST,HMPFILT,HMPTYPE,HMPOP,HMPLIST2,VPERR)=""
 I +$G(HMPIEN)'>0 S VPERR="0^Invalid Roster IEN" Q
 S HMPRNAME=$P($G(^HMPROSTR(HMPIEN,0)),"^",1) I HMPRNAME="" S VPERR="0^Deleted Roster IEN" Q
 F  S HMPIZ=$O(^HMPROSTR(HMPIEN,1,HMPIZ)) Q:HMPIZ'>0  D
 . S HMPX=$G(^HMPROSTR(HMPIEN,1,HMPIZ,0))
 . S HMPOP=$P(HMPX,"^",3)
 . S HMPFILT=$P(HMPX,"^",4)
 . S HMPTAG=$P($P(HMPX,"^",2),";",2)
 . S HMPLAB=""
 . I HMPTAG["SC(" S HMPLAB="CLIN"
 . I HMPTAG["DIC(42" S HMPLAB="WARD"
 . I HMPTAG["DPT" S HMPLAB="PAT"
 . I HMPTAG["SCTM" S HMPLAB="PCMM"
 . I HMPTAG["OR(100.21" S HMPLAB="CPRS"
 . I HMPTAG["HMPROSTR" S HMPLAB="ROST"
 . I HMPTAG["DIC(45.7" S HMPLAB="SPEC"
 . I HMPTAG["VA(200" S HMPLAB="PROV"
 . I HMPTAG["PXRM(810.4" S HMPLAB="PXRM"
 . I HMPLAB="" S VPERR="1^INVALID FILE TYPE" Q
 . D @HMPLAB
 . S HMPLAB=$S(HMPOP=0:"UNION",HMPOP=1:"INTER",1:"DIFF")
 . S HMPNLIST=""
 . D @HMPLAB
 M HMPLIST2=HMPLIST
 D GENPAT(HMPIEN)
 Q
 ;
CLIN ;Process patients for this clinic.  Select all if filter is null
 K HMPLIST2 S HMPLIST2=""
 I '$D(DT) S DT=$$DT^XLFDT()
 S BEG=DT,END=$S(HMPFILT="T":DT+.24,1:9999999+.24),HMPIII=BEG
 S HMPCIEN=+$P(HMPX,"^",2) F  S HMPIII=$O(^SC(HMPCIEN,"S",HMPIII)) Q:HMPIII'>0!(HMPIII>END)  D
 . S HMPII=0 F  S HMPII=$O(^SC(HMPCIEN,"S",HMPIII,1,HMPII)) Q:HMPII'>0  S DFN=$P($G(^SC(HMPCIEN,"S",HMPIII,1,HMPII,0)),"^",1) I DFN>0 D
 . .S HMPLIST2(DFN)=HMPIZ
 Q
 ;
WARD ;Process patients for this ward
 K HMPLIST2 S HMPLIST2=""
 S HMPWIEN=+$P(HMPX,"^",2),HMPWNAME=$P($G(^DIC(42,HMPWIEN,0)),"^",1)
 S HMPIII=0 F  S HMPIII=$O(^DGPM("CN",HMPWNAME,HMPIII)) Q:HMPIII'>0  D
 . S DFN=$P($G(^DGPM(HMPIII,0)),"^",3),HMPLIST2(DFN)=HMPIZ
 Q
 ;
PAT ;Process patient from Patient file Source
 K HMPLIST2 S HMPLIST2=""
 S DFN=+$P(HMPX,"^",2),HMPLIST2(DFN)=HMPIZ
 Q
 ;
PCMM ;Process patients from a PCMM team
 K HMPLIST2 S HMPLIST2=""
 S HMPTIEN=+$P(HMPX,"^",2),VPERR="",HMPTLST=""
 D PTTM^SCAPMC(HMPTIEN,,"HMPTLST",VPERR)
 S HMPIII="" F  S HMPIII=$O(HMPTLST(HMPIII)) Q:HMPIII'>0  S DFN=$P(HMPTLST(HMPIII),"^",1) S HMPTST2(DFN)=HMPIZ
 Q
 ;
CPRS ;Process patients from CPRS Lists
 K HMPLIST2 S HMPLIST2=""
 S HMPOIEN=+$P(HMPX,"^",2),VPERR=""
 S HMPIII=0 F  S HMPIII=$O(^OR(100.21,HMPOIEN,10,HMPIII)) Q:HMPIII'>0  S DFN=$P(^OR(100.21,HMPOIEN,10,HMPIII,0),";",1) S HMPLIST2(DFN)=HMPIZ
 Q
 ;
ROST ;Process patients from selected roster
 K HMPLIST2,HMPBLIST S (HMPLIST2,HMPBLIST)="" ; -- kcm added comma
 N HMP,HMPIEN,VPERR
 S HMPIEN=+$P(HMPX,"^",2),VPERR="",HMPOUT=1,HMP="HMPBLIST"
 D COMPILE^HMPROS2(.HMPZ,HMPIEN,"")
 M HMPBLIST=HMPLIST2
 K HMPOUT
 Q
 ;
SPEC ;Process patients with selected Treating Specialty
 K HMPLIST2 S HMPLIST2=""
 S HMPOIEN=+$P(HMPX,"^",2),VPERR=""
 N DFN S DFN=0 F  S DFN=$O(^DPT("ATR",HMPOIEN,DFN)) Q:DFN'>0  S HMPLIST2(DFN)=HMPIZ
 Q
 ;
PROV ;Process patients for selected provider
 K HMPLIST2 S HMPLIST2=""
 S HMPPIEN=+$P(HMPX,"^",2),VPERR=""
 N DFN S DFN=0 F  S DFN=$O(^DPT("APR",HMPPIEN,DFN)) Q:DFN'>0  S HMPLIST2(DFN)=""
 Q
 ;
PXRM ;Process patients for selected panel
 K HMPLIST2 S HMPLIST2=""
 S HMPPIEN=+$P(HMPX,"^",2),VPERR=""
 S HMPC=HMPPIEN,HMPLIEN=$P(^HMPROSTR(HMPIEN,0),"^",1),HMPPNME=$P(^HMPROSTR(HMPIEN,0),"^",6) I HMPPNME="" S HMPPNME=HMPRNAME,$P(^HMPROSTR(HMPIEN,0),U,6)=HMPRNAME
 S HMPPAT="" D RUNLIST^HMPROS5(.HMPPAT,HMPLIEN,HMPPNME,0,1)
 S HMPII=0 F  S HMPII=$O(HMPPAT(HMPC,HMPII)) Q:HMPII'>0  S DFN=HMPPAT(HMPC,HMPII),HMPLIST2(DFN)=HMPIZ
 Q
 ;
UNION ;Add to existing list
 S HMPII=0 F  S HMPII=$O(HMPLIST2(HMPII)) Q:HMPII'>0  S HMPLIST(HMPII)=HMPLIST2(HMPII)
 Q
 ;
INTER ;Intersect with existing list
 S HMPII=0 F  S HMPII=$O(HMPLIST(HMPII)) Q:HMPII'>0  D
 . I '$D(HMPLIST2(HMPII)) K HMPLIST(HMPII)
 Q
 ;
DIFF ;Remove patients from this source that we have so far
 S HMPII=0 F  S HMPII=$O(HMPLIST2(HMPII)) Q:HMPII'>0  D
 . K HMPLIST(HMPII)
 Q
 ;
GENPAT(HMPID) ;
 N DFN,DIC,DR,DIE,DA,NODE,HMPNUM
 S DFN=0
 S HMPNUM=0
 K ^HMPROSTR(HMPID,4) S ^HMPROSTR(HMPID,4,0)="^800001.23P^^"
 F  S DFN=$O(HMPLIST(DFN)) Q:DFN'>0  D
 . S HMPNUM=HMPNUM+1 S ^HMPROSTR(HMPID,4,HMPNUM,0)=DFN_"^"_HMPLIST(DFN),^HMPROSTR(HMPID,4,"B",DFN,HMPNUM)="",^HMPROSTR("AB",DFN,HMPID,HMPNUM)=""
 Q
